{
  outputs,
  inputs,
}:
let
  addPatches =
    pkg: patches:
    pkg.overrideAttrs (oldAttrs: {
      patches = (oldAttrs.patches or [ ]) ++ patches;
    });
in
rec {
  modifications =
    final: prev:
    let
      masterPkgs = import inputs.nixpkgs-master {
        system = final.stdenv.hostPlatform.system;
        config.allowUnfree = true;
      };
      stablePkgs = import inputs.nixpkgs-stable {
        system = final.stdenv.hostPlatform.system;
        config.allowUnfree = true;
      };
    in
    {
      # packages where we use master by default for bleeding edge

      # 2026-04-01 Claude package 2.1.88 yanked from npm, so had to revert until master updates
      # claude-code = masterPkgs.claude-code;
      discord = masterPkgs.discord;
      opencode = masterPkgs.opencode;

      # packages not yet in nixpkgs; use local definitions
      # playwright-cli depends on chromium which is Linux-only
      playwright-cli =
        if final.stdenv.isLinux then final.callPackage ../pkgs/playwright-cli.nix { } else null;

      prism = final.callPackage ../pkgs/prism.nix { };

      forgecode =
        if final.stdenv.isLinux || final.stdenv.isDarwin then
          final.callPackage ../pkgs/forgecode.nix { }
        else
          null;

      _macronTypePkg =
        if final.stdenv.isDarwin then final.callPackage ../pkgs/macron-type.nix { } else null;
      macron-type = if final.stdenv.isDarwin then final._macronTypePkg.server else null;
      macron-send = if final.stdenv.isDarwin then final._macronTypePkg.client else null;

    };
}
